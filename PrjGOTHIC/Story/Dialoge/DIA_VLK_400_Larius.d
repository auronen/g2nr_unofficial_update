
instance DIA_Larius_EXIT(C_Info)
{
	npc = VLK_400_Larius;
	nr = 999;
	condition = DIA_Larius_EXIT_Condition;
	information = DIA_Larius_EXIT_Info;
	permanent = TRUE;
	description = Dialog_Ende;
};


func int DIA_Larius_EXIT_Condition()
{
	return TRUE;
};

func void DIA_Larius_EXIT_Info()
{
	AI_StopProcessInfos(self);
};


instance DIA_Larius_Hello(C_Info)
{
	npc = VLK_400_Larius;
	nr = 1;
	condition = DIA_Larius_Hello_Condition;
	information = DIA_Larius_Hello_Info;
	permanent = FALSE;
	important = TRUE;
};


func int DIA_Larius_Hello_Condition()
{
	if(Npc_IsInState(self,ZS_Talk))
	{
		return TRUE;
	};
};

func void DIA_Larius_Hello_Info()
{
	AI_Output(self,other,"DIA_Larius_Hello_01_00");	//Что ты делаешь здесь? Если тебе что-то нужно, запишись на прием.
	if(VisibleGuild(other) == GIL_PAL)
	{
		AI_Output(self,other,"DIA_Larius_Richterueberfall_01_02");	//Даже если ты воин Инноса...
	}
	else if((VisibleGuild(other) == GIL_KDF) || (VisibleGuild(other) == GIL_KDW))
	{
		AI_Output(self,other,"DIA_Larius_Richterueberfall_01_03");	//Даже если ты посвященный маг...
	};
};


instance DIA_Larius_WhoAreYou(C_Info)
{
	npc = VLK_400_Larius;
	nr = 1;
	condition = DIA_Larius_WhoAreYou_Condition;
	information = DIA_Larius_WhoAreYou_Info;
	permanent = FALSE;
	description = "Кто ты?";
};


func int DIA_Larius_WhoAreYou_Condition()
{
	return TRUE;
};

func void DIA_Larius_WhoAreYou_Info()
{
	AI_Output(other,self,"DIA_Larius_WhoAreYou_15_00");	//Кто ты?
	AI_Output(self,other,"DIA_Larius_WhoAreYou_01_01");	//Я Лариус, губернатор.
	AI_Output(self,other,"DIA_Larius_WhoAreYou_01_02");	//Даже если я не кажусь тебе таковым в данный момент, я самый влиятельный человек в этом городе.
	AI_Output(other,self,"DIA_Larius_WhoAreYou_15_03");	//То есть ты всем здесь заправляешь?
	AI_Output(self,other,"DIA_Larius_WhoAreYou_01_04");	//Я... ну... в настоящий момент мои руки связаны.
	AI_Output(self,other,"DIA_Larius_WhoAreYou_01_05");	//Здесь командует этот лорд Хаген, по крайней мере, пока он в городе.
	Player_KnowsLordHagen = TRUE;
	Player_KnowsLariusAsGovernor = TRUE;
};


instance DIA_Larius_Disturb(C_Info)
{
	npc = VLK_400_Larius;
	nr = 2;
	condition = DIA_Larius_Disturb_Condition;
	information = DIA_Larius_Disturb_Info;
	permanent = TRUE;
	description = "Я не хотел помешать.";
};


func int DIA_Larius_Disturb_Condition()
{
	return TRUE;
};

func void DIA_Larius_Disturb_Info()
{
	AI_Output(other,self,"DIA_Larius_Disturb_15_00");	//Я не хотел помешать.
	if(!Npc_KnowsInfo(other,DIA_Larius_Dragons_Proof))
	{
		AI_Output(self,other,"DIA_Larius_Disturb_01_01");	//Но, тем не менее, помешал! Убирайся!
	}
	else
	{
		B_Say(self,other,"$ABS_GOOD");
	};
};


instance DIA_Larius_DieLage(C_Info)
{
	npc = VLK_400_Larius;
	nr = 3;
	condition = DIA_Larius_DieLage_Condition;
	information = DIA_Larius_DieLage_Info;
	permanent = TRUE;
	description = "Как дела?";
};


func int DIA_Larius_DieLage_Condition()
{
	return TRUE;
};

func void DIA_Larius_DieLage_Info()
{
	AI_Output(other,self,"DIA_Larius_DieLage_15_00");	//Как дела?
	if(!Npc_KnowsInfo(other,DIA_Larius_Dragons_Proof))
	{
		AI_Output(self,other,"DIA_Larius_DieLage_01_01");	//Тебе до этого какое дело? Пока паладины здесь, лорд Хаген занимается всеми делами, касающимися жизни города.
	}
	else
	{
		AI_Output(self,other,"DIA_Lothar_Add_01_47");	//Опять ты.
	};
};


instance DIA_Larius_Richterueberfall(C_Info)
{
	npc = VLK_400_Larius;
	nr = 4;
	condition = DIA_Larius_Richterueberfall_Condition;
	information = DIA_Larius_Richterueberfall_Info;
	description = "Судья нанял бандитов, чтобы они убили тебя. Я могу доказать это.";
};


func int DIA_Larius_Richterueberfall_Condition()
{
	if(SCKnowsRichterKomproBrief == TRUE)
	{
		return TRUE;
	};
};

func void DIA_Larius_Richterueberfall_Info()
{
	AI_Output(other,self,"DIA_Larius_Richterueberfall_15_00");	//Судья нанял бандитов, чтобы они убили тебя. Я могу доказать это.
	AI_Output(self,other,"DIA_Larius_Richterueberfall_01_01");	//Не пори чепухи, или ты хочешь, чтобы я заковал тебя в кандалы?
	AI_Output(self,other,"DIA_Larius_Richterueberfall_01_04");	//Мое слово все еще имеет вес в этом городе. Не смей даже пытаться запятнать светлое имя нашего судьи!
	if(Npc_KnowsInfo(other,DIA_Larius_Dragons_Proof))
	{
		AI_Output(other,self,"DIA_Sekob_Heilung_15_02");	//(себе под нос) Тяжелый случай.
	};
	B_GivePlayerXP(XP_Ambient);
	AI_StopProcessInfos(self);
};


instance DIA_Larius_Dragons(C_Info)
{
	npc = VLK_400_Larius;
	nr = 1;
	condition = DIA_Larius_Dragons_Condition;
	information = DIA_Larius_Dragons_Info;
	description = "Послушай - этому городу угрожают драконы!";
};


func int DIA_Larius_Dragons_Condition()
{
	if((Player_KnowsLariusAsGovernor == TRUE) && (Kapitel < 5))
	{
		return TRUE;
	};
};

func void DIA_Larius_Dragons_Info()
{
	AI_Output(other,self,"DIA_Lothar_Dragons_15_00");	//Послушай - этому городу угрожают драконы!
	AI_Output(self,other,"DIA_Larius_Richterueberfall_01_01");	//Не пори чепухи, или ты хочешь, чтобы я заковал тебя в кандалы?
	B_Say(self,other,"$GETUPANDBEGONE");
	if(Npc_KnowsInfo(other,DIA_Lothar_Dragons))
	{
		Player_TalkedAboutDragonsToSomeone = TRUE;
	};
	AI_StopProcessInfos(self);
};


instance DIA_Larius_Dragons_Proof(C_Info)
{
	npc = VLK_400_Larius;
	nr = 1;
	condition = DIA_Larius_Dragons_Proof_Condition;
	information = DIA_Larius_Dragons_Proof_Info;
	description = "У меня есть доказательство! Вот письмо от командующего Гаронда!";
};


func int DIA_Larius_Dragons_Proof_Condition()
{
	if(Npc_KnowsInfo(other,DIA_Larius_Dragons) && Npc_HasItems(other,ItWr_PaladinLetter_MIS))
	{
		return TRUE;
	};
};

func void DIA_Larius_Dragons_Proof_Info()
{
	DIA_Lothar_OWRunningBrief_Info();
	B_GivePlayerXP(100);
};

