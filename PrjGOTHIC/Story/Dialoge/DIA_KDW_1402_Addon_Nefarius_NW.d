
instance DIA_Addon_Nefarius_EXIT(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 999;
	condition = DIA_Addon_Nefarius_EXIT_Condition;
	information = DIA_Addon_Nefarius_EXIT_Info;
	permanent = TRUE;
	description = Dialog_Ende;
};


func int DIA_Addon_Nefarius_EXIT_Condition()
{
	return TRUE;
};

func void DIA_Addon_Nefarius_EXIT_Info()
{
	AI_StopProcessInfos(self);
};


instance DIA_Addon_Nefarius_Hallo(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 1;
	condition = DIA_Addon_Nefarius_Hallo_Condition;
	information = DIA_Addon_Nefarius_Hallo_Info;
//	description = "Как дела?";
	important = TRUE;
};


func int DIA_Addon_Nefarius_Hallo_Condition()
{
	return TRUE;
};

func void DIA_Addon_Nefarius_Hallo_Info()
{
//	AI_Output(other,self,"DIA_Addon_Nefarius_Hallo_15_00");	//Как дела?
	AI_Output(self,other,"DIA_Addon_Nefarius_Hallo_05_01");	//А что ТЫ здесь делаешь? Вот это сюрприз!
	AI_Output(self,other,"DIA_Addon_Nefarius_Hallo_05_02");	//Я думал, ты погиб.
	AI_Output(other,self,"DIA_Addon_Nefarius_Hallo_15_03");	//Почти.
	AI_Output(self,other,"DIA_Addon_Nefarius_Hallo_05_04");	//Из-за тебя поднялся большой шум, ты знаешь об этом? Сатураса было не узнать - те события полностью вывели его из себя.
};


instance DIA_Addon_Nefarius_keineahnung(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_keineahnung_Condition;
	information = DIA_Addon_Nefarius_keineahnung_Info;
	description = "Что это за портал?";
};


func int DIA_Addon_Nefarius_keineahnung_Condition()
{
	if(Npc_KnowsInfo(other,DIA_Addon_Nefarius_Hallo))
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_keineahnung_Info()
{
	AI_Output(other,self,"DIA_Addon_Nefarius_keineahnung_15_00");	//Что это за портал?
	AI_Output(self,other,"DIA_Addon_Nefarius_keineahnung_05_01");	//Мы считаем, что он ведет в затерянную долину, в которой находится город древней цивилизации.
	if(Portal_Activated == FALSE)
	{
		AI_Output(self,other,"DIA_Addon_Nefarius_keineahnung_05_02");	//Но пока за порталом находится лишь многометровая толща камня.
		AI_Output(self,other,"DIA_Addon_Nefarius_keineahnung_05_03");	//Никаких следов магии телепортации нам обнаружить не удалось. Очень загадочно...
	};
	if(SC_KnowsPortal == FALSE)
	{
		Log_CreateTopic(TOPIC_Addon_KDW,LOG_MISSION);
		Log_SetTopicStatus(TOPIC_Addon_KDW,LOG_Running);
		B_LogEntry(TOPIC_Addon_KDW,"Маги Воды считают, что за порталом находится древний затерянный город.");
		SC_KnowsPortal = TRUE;
	};
};


func void B_Nefarius_PortalKeyTip()
{
	AI_Output(self,other,"DIA_Addon_Nefarius_WieMechanik_05_02");	//Ключ должен точно войти в кольцевидное углубление рядом с порталом.
};

instance DIA_Addon_Nefarius_WieMechanik(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_WieMechanik_Condition;
	information = DIA_Addon_Nefarius_WieMechanik_Info;
	description = "Ты знаешь, как активировать портал?";
};


func int DIA_Addon_Nefarius_WieMechanik_Condition()
{
	if(Npc_KnowsInfo(other,DIA_Addon_Nefarius_keineahnung) && (Portal_Activated == FALSE))
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_WieMechanik_Info()
{
	AI_Output(other,self,"DIA_Addon_Nefarius_WieMechanik_15_00");	//Ты знаешь, как активировать портал?
	if(RitualRingRuns != LOG_SUCCESS)
	{
		AI_Output(self,other,"DIA_Addon_Nefarius_WieMechanik_05_01");	//Похоже, что пропавшие части орнамента складываются в ключ.
		AI_Output(self,other,"DIA_Addon_Nefarius_WieMechanik_05_03");	//Он-то и нужен нам, чтобы открыть портал.
	};
	B_Nefarius_PortalKeyTip();
};


instance DIA_Addon_Nefarius_SCbringOrnaments(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_SCbringOrnaments_Condition;
	information = DIA_Addon_Nefarius_SCbringOrnaments_Info;
	description = "Я принес часть орнамента.";
};


func int DIA_Addon_Nefarius_SCbringOrnaments_Condition()
{
	if(MIS_Addon_Lares_Ornament2Saturas == LOG_SUCCESS)
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_SCbringOrnaments_Info()
{
	AI_Output(other,self,"DIA_Addon_Nefarius_SCbringOrnaments_15_00");	//Я принес часть орнамента.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_01");	//Правда? Это замечательно.
	AI_Output(other,self,"DIA_Addon_Nefarius_SCbringOrnaments_15_02");	//Ты знаешь, где остальные части?
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_03");	//Если верить древним письменам, ключ был разделен на четыре части.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_04");	//Эти части все еще находятся на острове...
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_05");	//С помощью Мердариона мне удалось расшифровать некоторые надписи.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_06");	//Они указывают на места, где были спрятаны части орнамента.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_07");	//Эти места я отметил вот на этой карте.
	CreateInvItems(self,ItWr_Map_NewWorld_Ornaments_Addon,1);
	B_GiveInvItems(self,other,ItWr_Map_NewWorld_Ornaments_Addon,1);
	MIS_Addon_Nefarius_BringMissingOrnaments = LOG_Running;
	Log_CreateTopic(TOPIC_Addon_Ornament,LOG_MISSION);
	Log_SetTopicStatus(TOPIC_Addon_Ornament,LOG_Running);
	B_LogEntry(TOPIC_Addon_Ornament,"Маги Воды нашли портал, который ведет в неизвестную часть Хориниса.");
	Log_AddEntry(TOPIC_Addon_Ornament,"Нефариус хочет активировать портал при помощи украшенного кольца. Ему все еще не хватает трех частей этого кольца. Я должен найти их. Он дал мне карту, на которой отмечены места, где я должен искать фрагменты.");
	SC_KnowsPortal = TRUE;
	if((Kapitel >= 3) && (hero.guild == GIL_KDF) && (Vino_isAlive_Kap3 == TRUE) && !C_VinoDementorsDead())
	{
		B_StartOtherRoutine(Cavalorn,"OrnamentSteinringCh3KDF");
	}
	else
	{
		B_StartOtherRoutine(Cavalorn,"OrnamentSteinring");
	};
	if(!Npc_IsDead(Ambusher_1013) && (Bdt_1013_FromCavalorn == TRUE) && (Bdt_1013_ToCavalorn == FALSE))
	{
		Npc_ExchangeRoutine(Ambusher_1013,"AWAY");
		Bdt_1013_ToCavalorn = TRUE;
	};
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_08");	//Ты должен искать части ключа именно там.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_09");	//Обращай внимание на древние постройки. Они могут быть полуразрушенными, но все же легко узнаваемыми.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_10");	//Это может быть что угодно: валун, круг камней, мавзолей или что-нибудь подобное.
	AI_Output(self,other,"DIA_Addon_Nefarius_SCbringOrnaments_05_11");	//Но к чему эта болтовня? Ты сам все увидишь. Удачи!
};


instance DIA_Addon_Nefarius_WhyPortalClosed(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_WhyPortalClosed_Condition;
	information = DIA_Addon_Nefarius_WhyPortalClosed_Info;
	description = "Для чего орнамент был разбит?";
};


func int DIA_Addon_Nefarius_WhyPortalClosed_Condition()
{
	if(Npc_KnowsInfo(other,DIA_Addon_Nefarius_SCbringOrnaments))
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_WhyPortalClosed_Info()
{
	AI_Output(other,self,"DIA_Addon_Nefarius_WhyPortalClosed_15_00");	//Для чего орнамент был разбит?
	AI_Output(self,other,"DIA_Addon_Nefarius_WhyPortalClosed_05_01");	//Тот, кто сделал это, не хотел, чтобы портал открыли снова.
	AI_Output(self,other,"DIA_Addon_Nefarius_WhyPortalClosed_05_02");	//Чем больше я изучаю эти надписи, тем больше убеждаюсь, что жрецы этой цивилизации не были дураками.
	AI_Output(self,other,"DIA_Addon_Nefarius_WhyPortalClosed_05_03");	//У них были веские причины закрыть доступ в свою долину.
	AI_Output(self,other,"DIA_Addon_Nefarius_WhyPortalClosed_05_04");	//Мы не знаем, что нас там ждет, но ничем хорошим это точно не окажется...
};


instance DIA_Addon_Nefarius_MissingOrnaments(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_MissingOrnaments_Condition;
	information = DIA_Addon_Nefarius_MissingOrnaments_Info;
	permanent = TRUE;
	description = "Насчет пропавших фрагментов...";
};


func int DIA_Addon_Nefarius_MissingOrnaments_Condition()
{
	if(MIS_Addon_Nefarius_BringMissingOrnaments == LOG_Running)
	{
		return TRUE;
	};
};


var int MissingOrnamentsCounter;

func void DIA_Addon_Nefarius_MissingOrnaments_Info()
{
	var int amount;
	amount = Npc_HasItems(other,ItMi_Ornament_Addon);
	AI_Output(other,self,"DIA_Addon_Nefarius_MissingOrnaments_15_00");	//Насчет пропавших фрагментов...
	AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_01");	//Да?
	if(amount > 0)
	{
		if(amount == 1)
		{
			AI_Output(other,self,"DIA_Addon_Nefarius_MissingOrnaments_15_02");	//Я нашел еще один.
		}
		else
		{
			AI_Output(other,self,"DIA_Addon_Nefarius_MissingOrnaments_15_03");	//Я нашел их.
		};
		B_GiveInvItems(other,self,ItMi_Ornament_Addon,amount);
		B_RemoveEveryInvItem(self,ItMi_Ornament_Addon);
		MissingOrnamentsCounter += amount;
		AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_04");	//Отлично!
		AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_09");	//Вот, возьми в награду это золото.
		B_GiveInvItems(self,other,ItMi_Gold,NefariusMissingOrnamentsOffer * amount);
		B_GivePlayerXP(XP_Addon_BringMissingOrnament * amount);
		if(MissingOrnamentsCounter == 1)
		{
			AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_05");	//Осталось найти еще два. Думаю, ты справишься.
		}
		else if(MissingOrnamentsCounter == 2)
		{
			AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_06");	//Найди еще один, и они будут у нас все.
		}
		else
		{
			AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_07");	//Теперь у нас есть все части. Осталось их собрать.
			AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_08");	//Мы используем наши объединенные силы, чтобы сделать из фрагментов кольцо.
			AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_10");	//Следуй за мной!
			B_LogEntry(TOPIC_Addon_Ornament,"Я принес все части кольца Нефариусу. Теперь маги Воды смогут собрать кольцо.");
			MIS_Addon_Nefarius_BringMissingOrnaments = LOG_SUCCESS;
			AI_StopProcessInfos(self);
			Npc_ExchangeRoutine(self,"PreRingritual");
			B_StartOtherRoutine(Saturas_NW,"PreRingritual");
			B_StartOtherRoutine(Cronos_NW,"PreRingritual");
			B_StartOtherRoutine(Myxir_NW,"PreRingritual");
			B_StartOtherRoutine(Riordian_NW,"PreRingritual");
			B_StartOtherRoutine(Merdarion_NW,"PreRingritual");
		};
	}
	else
	{
		AI_Output(other,self,"DIA_Addon_Nefarius_MissingOrnaments_15_11");	//Напомни, сколько всего фрагментов?
		AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_12");	//Четыре...
		AI_Output(self,other,"DIA_Addon_Nefarius_MissingOrnaments_05_13");	//Ты найдешь оставшиеся фрагменты в местах, которые я отметил на карте.
	};
};


instance DIA_Addon_Nefarius_Ringritual(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_Ringritual_Condition;
	information = DIA_Addon_Nefarius_Ringritual_Info;
	important = TRUE;
};


func int DIA_Addon_Nefarius_Ringritual_Condition()
{
	if((MIS_Addon_Nefarius_BringMissingOrnaments == LOG_SUCCESS) && (Npc_GetDistToWP(self,"NW_TROLLAREA_PORTALTEMPEL_42") < 1000))
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_Ringritual_Info()
{
	AI_Output(self,other,"DIA_Addon_Nefarius_Ringritual_05_00");	//А теперь отойди назад.
	Npc_SetRefuseTalk(self,60);
	RitualRingRuns = LOG_Running;
	AI_StopProcessInfos(self);
	Npc_ExchangeRoutine(self,"Ringritual");
	B_StartOtherRoutine(Saturas_NW,"Ringritual");
	B_StartOtherRoutine(Cronos_NW,"Ringritual");
	B_StartOtherRoutine(Myxir_NW,"Ringritual");
	B_StartOtherRoutine(Riordian_NW,"Ringritual");
	B_StartOtherRoutine(Merdarion_NW,"Ringritual");
};


instance DIA_Addon_Nefarius_RingRitualEnds(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_RingRitualEnds_Condition;
	information = DIA_Addon_Nefarius_RingRitualEnds_Info;
	important = TRUE;
};


func int DIA_Addon_Nefarius_RingRitualEnds_Condition()
{
	if(!Npc_RefuseTalk(self) && (RitualRingRuns == LOG_Running))
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_RingRitualEnds_Info()
{
	AI_Output(self,other,"DIA_Addon_Nefarius_RingRitualEnds_05_00");	//Орнамент снова собран.
	AI_Output(self,other,"DIA_Addon_Nefarius_RingRitualEnds_05_01");	//Ты принес нам недостающие фрагменты, поэтому мы удостаиваем тебя чести вставить ключ в портал.
	AI_Output(self,other,"DIA_Addon_Nefarius_RingRitualEnds_05_02");	//Иди к Сатурасу и забери у него кольцо.
	AI_Output(self,other,"DIA_Addon_Nefarius_RingRitualEnds_05_03");	//Надеюсь, портал откроется.
	AI_StopProcessInfos(self);
	Npc_ExchangeRoutine(self,"Start");
	B_StartOtherRoutine(Saturas_NW,"Start");
	B_StartOtherRoutine(Cronos_NW,"Start");
	B_StartOtherRoutine(Myxir_NW,"Start");
	B_StartOtherRoutine(Riordian_NW,"Start");
	B_StartOtherRoutine(Merdarion_NW,"Start");
	RitualRingRuns = LOG_SUCCESS;
	B_LogEntry(TOPIC_Addon_Ornament,"Украшенное кольцо восстановлено. Я должен забрать его у Сатураса.");
};


instance DIA_Addon_Nefarius_OpenedPortal(C_Info)
{
	npc = KDW_1402_Addon_Nefarius_NW;
	nr = 5;
	condition = DIA_Addon_Nefarius_OpenedPortal_Condition;
	information = DIA_Addon_Nefarius_OpenedPortal_Info;
	description = "Что теперь?";
};


func int DIA_Addon_Nefarius_OpenedPortal_Condition()
{
	if(RitualRingRuns == LOG_SUCCESS)
	{
		return TRUE;
	};
};

func void DIA_Addon_Nefarius_OpenedPortal_Info()
{
	AI_Output(other,self,"DIA_Addon_Nefarius_OpenedPortal_15_00");	//Что теперь?
	if(Portal_Activated == FALSE)
	{
		B_Nefarius_PortalKeyTip();
	}
	else
	{
		AI_Output(self,other,"DIA_Addon_Nefarius_OpenedPortal_05_01");	//Чего ты ждешь? Отойди с дороги.
	};
};

